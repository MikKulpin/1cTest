//Изменения для тестирования Git
&НаКлиенте
Процедура Печать(Команда)
	
	Если Вариант = "ПолнаяКалькуляция" Тогда
		Обработчик = "ОбщепитДокументыКлиентМайбокс.ВыполнитьКомандуПечати_РецептураОП1СПараметрами";
		Представление = "Калькуляционная карточка ОП-1";
		Идентификатор = "ОбщепитОП1";
	ИначеЕсли Вариант = "ПолнаяКалькуляцияКоновалов" Тогда
		Обработчик = "ОбщепитДокументыКлиентМайбокс.ВыполнитьКомандуПечати_РецептураОП1СПараметрами_Коновалов";
		Представление = "Калькуляционная карточка ОП-1";
		Идентификатор = "ОбщепитОП1";
	ИначеЕсли Вариант = "ЦеныБлюд" Тогда
		Обработчик = "ОбщепитДокументыКлиентМайбокс.ВыполнитьКомандуПечати_ОбщепитЦеныБлюд";
		Представление = "Цены блюд";
		Идентификатор = "ОбщепитЦеныБлюд";
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Неверный вариант печатной формы. Выберите вариант из списка";
		Сообщение.Поле = "Вариант";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	МассивРецептур = ПолучитьМассивРецептур();


	Если МассивРецептур.Количество() > 0 Тогда
		ОписаниеКоманды = Новый Структура;
		ОписаниеКоманды.Вставить("Вид","Печать");
		ОписаниеКоманды.Вставить("Идентификатор",Идентификатор);
		ОписаниеКоманды.Вставить("ИзменяетВыбранныеОбъекты",Ложь);
		ОписаниеКоманды.Вставить("МенеджерПечати","Документ.ОбщепитРецептура");
		ОписаниеКоманды.Вставить("МножественныйВыбор",Истина);
		ОписаниеКоманды.Вставить("Обработчик",Обработчик);
		ОписаниеКоманды.Вставить("ОбъектыПечати",МассивРецептур);
		ОписаниеКоманды.Вставить("Представление",Представление);
		ОписаниеКоманды.Вставить("ТипПараметра", Новый ОписаниеТипов("ДокументСсылка.ОбщепитРецептура"));
		ОписаниеКоманды.Вставить("Форма", ЭтаФорма);

		Выполнить(Обработчик+"(ОписаниеКоманды)");

	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивРецептур()
	
	МассивРецептур = Новый Массив;
	Для Каждого Номенклатура Из СписокНоменклатуры Цикл
		Если Номенклатура.Значение.ЭтоГруппа Тогда
			МассивНоменклатуры = ПолучитьПодчиненнуюНоменклатуру(Номенклатура.Значение);
			Для Каждого ЭлементМассива Из МассивНоменклатуры Цикл
				ДобавитьРецептуруВМассив(МассивРецептур, ЭлементМассива);
			КонецЦикла;
		Иначе
			ДобавитьРецептуруВМассив(МассивРецептур, Номенклатура.Значение);
		КонецЕсли;
	КонецЦикла;
	Возврат МассивРецептур;

КонецФункции

&НаСервере
Функция ПолучитьПодчиненнуюНоменклатуру(Группа)
	
	Результат = Новый Массив;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Номенклатура.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Ссылка В ИЕРАРХИИ(&Группа)
	               |	И НЕ Номенклатура.ПометкаУдаления
	               |	И НЕ Номенклатура.ЭтоГруппа";
	Запрос.УстановитьПараметр("Группа",Группа);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат Результат
КонецФункции

&НаСервере
Процедура ДобавитьРецептуруВМассив(МассивРецептур, Номенклатура)
	
	Перем Рецептура;
	
	Рецептура = ОбщепитПроизводствоМайбокс.ВернутьОсновнуюРецептуру(Номенклатура,ДатаАктуальности);
	Если ЗначениеЗаполнено(Рецептура) И МассивРецептур.Найти(Рецептура) = Неопределено Тогда
		МассивРецептур.Добавить(Рецептура);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Не ЗначениеЗаполнено(Вариант) Тогда
		Вариант = Элементы.Вариант.СписокВыбора[0].Значение;
	КонецЕсли;
КонецПроцедуры
